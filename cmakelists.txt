
cmake_minimum_required(VERSION 3.15)

project(oxygen VERSION 0.1 LANGUAGES CXX)

include(GNUInstallDirs)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

file(GLOB_RECURSE SOURCES "src/*.cpp")
add_library(oxygen STATIC ${SOURCES})

target_include_directories(oxygen
    PUBLIC
        $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
        $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
)

target_compile_features(oxygen PUBLIC cxx_std_17)

install(TARGETS oxygen
    EXPORT oxygenTargets
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
)

install(DIRECTORY include/ DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})

install(EXPORT oxygenTargets
    FILE oxygenTargets.cmake
    NAMESPACE oxygen::
    DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/oxygen
)

message(LOG "Installed in ${CMAKE_INSTALL_LIBDIR}")

include(CMakePackageConfigHelpers)

configure_package_config_file(
    cmake/oxygenConfig.cmake.in
    "${CMAKE_CURRENT_BINARY_DIR}/oxygenConfig.cmake"
    INSTALL_DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/oxygen
)

install(FILES "${CMAKE_CURRENT_BINARY_DIR}/oxygenConfig.cmake"
        DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/oxygen)

